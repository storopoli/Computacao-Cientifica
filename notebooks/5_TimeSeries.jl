### A Pluto.jl notebook ###
# v0.16.1

using Markdown
using InteractiveUtils

# This Pluto notebook uses @bind for interactivity. When running this notebook outside of Pluto, the following 'mock version' of @bind gives bound variables a default value (instead of an error).
macro bind(def, element)
    quote
        local el = $(esc(element))
        global $(esc(def)) = Core.applicable(Base.get, el) ? Base.get(el) : missing
        el
    end
end

# â•”â•â•¡ 27f62732-c909-11eb-27ee-e373dce148d9
begin
	using Pkg
	Pkg.activate(mktempdir())
	Pkg.add([
			"PlutoUI",
			"CSV",
			"DataFrames",
			"DataWrangler",
			"Forecast",
			"Plots",
			"RollingFunctions",
			"ShiftedArrays",
			"HTTP"
			])
	Pkg.add(url="https://github.com/pierrenodet/AutoARIMA.jl")
	
	using PlutoUI
	
	using AutoARIMA
	using Dates
	using DataFrames
	using DataWrangler
	using Forecast
	using Plots
	using RollingFunctions
	using ShiftedArrays
	
	using Statistics: mean
	using ShiftedArrays: lag, lead

	# evitar conflitos
	import HTTP # stack de DataFrames
	using DataFrames: transform
	using Base: isnothing
	using AutoARIMA: forecast
end

# â•”â•â•¡ 228e9bf1-cfd8-4285-8b68-43762e1ae8c7
begin
	using InteractiveUtils
	with_terminal() do
		versioninfo()
	end
end

# â•”â•â•¡ cbc48ca5-f1a4-4e13-9323-2fd2c43d8612
TableOfContents(aside=true)

# â•”â•â•¡ 7bb67403-d2ac-4dc9-b2f1-fdea7a795329
md"""
# SÃ©ries Temporais `DataFrames.jl`
"""

# â•”â•â•¡ ff1ec432-ce73-4d04-8dcf-dea133894b67
Resource("https://img.shields.io/badge/License-CC%20BY--SA%204.0-lightgrey.svg", :width => 120, :display => "inline")

# â•”â•â•¡ b09513de-7df4-4fd5-95c4-d38965d6a666
md"""
!!! danger "âš ï¸ Disciplina Ferramental"
    **Esta disciplina Ã© uma disciplina ferramental!**

	Portanto, se vocÃª nÃ£o sabe o que Ã© uma rede ou um grafo, pegue um livro-texto e estude ou pergunte pro seu orientador.
"""

# â•”â•â•¡ 59b60d4a-04bf-425e-a443-1361af55a428
md"""
# O que sÃ£o SÃ©ries Temporais?

Uma **sÃ©rie temporal** Ã© uma coleÃ§Ã£o de observaÃ§Ãµes feitas **sequencialmente** ao longo do tempo.
"""

# â•”â•â•¡ 9eb79017-d135-44e9-a2ea-712829418a6d
md"""
 $N$ = $(@bind N_1 Slider(25:25:300, default=25, show_value=true))
"""

# â•”â•â•¡ 38581a7b-2da4-49fb-b5e0-32db22cb3616
plot(Plots.fakedata(N_1), label=:none, lw=3, marker=:circle, xlab="\$N\$")

# â•”â•â•¡ 23e24ad3-296b-4316-bb1b-617262b93a9b
md"""
!!! info "ğŸ’ Por quÃª Series Temporais sÃ£o tÃ£o Especiais?"
    Dada a ordenaÃ§Ã£o temporal dos dados, as observaÃ§Ãµes nÃ£o possuem o pressuposto de independÃªncia, portanto hÃ¡ uma **dependÃªncia temporal** que precisamos lidar.
"""

# â•”â•â•¡ aa89ebab-573c-4819-b1c6-ffce3fff5a18
md"""
## Componentes de SÃ©ries Temporais

Geralmente uma sÃ©rie temporal tem alguns componentes:

$$y_t = S_t + T_t + R_t$$

Onde:

*  $y_t$ Ã© a observaÃ§Ã£o no tempo $t$.
*  $T_t$ Ã© o componente de **tÃªndencia** no tempo $t$.
*  $S_t$ Ã© o componente **sazonal** no tempo $t$.
*  $R_t$ Ã© o componente **residual** no tempo $t$.

AlÃ©m disso classificamos como:

* **EstacionÃ¡ria**: tambÃ©m chamada de *convergente*, flutua em torno de uma mesma mÃ©dia ao longo do tempo.
* **NÃ£o-EstacionÃ¡ria**: tambÃ©m chamada de *divergente*, evoluem ao longo do tempo.

"""

# â•”â•â•¡ f8ab9748-2002-4e48-82d2-60518e06f5cd
md"""
 $N$ = $(@bind N_2 Slider(50:50:600, default=300, show_value=true))
"""

# â•”â•â•¡ 5d701b31-c830-4097-95ef-0e257ea36cb0
md"""
!!! info "ğŸ’ VisualizaÃ§Ãµes de SÃ©ries Temporais"
    HÃ¡ alguns pacotes mais especializados sobre como visualizar sÃ©ries temporais. Em especial veja o [`TimeSeries.jl`](https://github.com/JuliaStats/TimeSeries.jl) e o [`Forecast.jl`](https://github.com/viraltux/Forecast.jl).
"""

# â•”â•â•¡ edc92911-9627-45d3-a340-4292ce4cfc3e
md"""
# Exemplo com o Dataset de Viagens Ãreas

Neste exemplo, preveremos os totais mensais em milhares de passageiros de companhias aÃ©reas internacionais com dados de 1949 a 1960.

Ele estÃ¡ presente no pacote [`Forecast.jl`](https://github.com/viraltux/Forecast.jl):

```julia
using Forecast

ap = air()
```

Tem apenas duas colunas: `Date`e `Passengers` (em milhares)
"""

# â•”â•â•¡ c958db84-9cb5-4398-b37a-15e8eb0b19f5
ap = air()

# â•”â•â•¡ 733645f3-70d2-4e75-9688-7170dd3e3d3b
plot(ap.Date, ap.Passengers; marker=:circle, ms=3, lw=3, label=false)

# â•”â•â•¡ 1a579b82-7552-4bba-bcaa-8eb507d34db4
md"""
!!! tip "ğŸ’¡ Tipos Date e DateTime"
    DÃª uma olhada mais atenta na coluna `Date` do dataset `ap`. Ela Ã© um tipo que nÃ£o vimos antes: `Date`.
"""

# â•”â•â•¡ e78c9151-baa3-4ec3-95bc-36843a42305c
describe(ap)

# â•”â•â•¡ d67b34ed-4fd8-4f4b-9e9b-027dcef8e05f
md"""
# MÃ³dulo `Dates`

Julia possui o mÃ³dulo [`Dates`](https://docs.julialang.org/en/v1/stdlib/Dates) na biblioteca padrÃ£o. Ou seja, jÃ¡ vem instalado em toda instalaÃ§Ã£o de Julia e basta dar um:

```julia
using Dates
```
"""

# â•”â•â•¡ d702c956-5765-433a-833a-fef06dacad8e
md"""
!!! info "ğŸ’ Como lidar com Datas e Tempo em Julia?"
    O `pandas` do Python usam seu prÃ³prio tipo `DateTime` para lidar com datas. O mesmo com o pacote `lubridate` do R `tidyverse`, que tambÃ©m define seu prÃ³prio tipo `datetime` para lidar com datas. Julia nÃ£o precisa de nada disso, ela tem todas as coisas de **datas jÃ¡ embutidas em sua biblioteca padrÃ£o, em um mÃ³dulo chamado `Dates`**.
"""

# â•”â•â•¡ 8131495e-f21a-4473-bf7d-beb84c145636
md"""
O mÃ³dulo da biblioteca padrÃ£o de datas tem dois tipos para trabalhar com datas:

* `Data`: representando o tempo em **dias**.
* `DateTime`: representando o tempo na precisÃ£o de **milissegundos**.

Podemos construir `Date` e `DateTime` com o construtor padrÃ£o, especificando um nÃºmero inteiro para representar ano, mÃªs, dia, horas e assim por diante:
"""

# â•”â•â•¡ 0e22fdf9-7106-4e03-b73e-df63e4612a8f
Date(1987) # ano

# â•”â•â•¡ baa42a2f-ac93-49ac-9af3-b794466f1b8f
Date(1987, 9) # mÃªs

# â•”â•â•¡ c085d76a-7d2e-48ea-820a-9baa303138b6
Date(1987, 9, 13) # dia

# â•”â•â•¡ 3b05adc5-8a00-4807-9418-071326f10e18
DateTime(1987, 9, 13, 21) # hora

# â•”â•â•¡ fef9271b-6956-4e5b-bc43-93501a05c2c1
DateTime(1987, 9, 13, 21, 21) # minuto

# â•”â•â•¡ 0f1481ac-a337-4ff1-bc2d-f810dc90fcfb
md"""
> Para os curiosos, 13/09/1987 21:21 Ã© a hora oficial de nascimento do professor da disciplina: JosÃ© Storopoli.
"""

# â•”â•â•¡ 15a25125-675a-4250-bfb5-56b982e63880
md"""
TambÃ©m podemos passar tipos de `Period` para o construtor padrÃ£o. **Os tipos de `Period` sÃ£o a representaÃ§Ã£o equivalente humana do tempo para o computador**. As datas de Julia tÃªm os seguintes subtipos abstratos de `Period`:
"""

# â•”â•â•¡ cb6a5372-b6b2-48c7-ad48-30023cceac80
subtypes(Period)

# â•”â•â•¡ a5e761fa-b3dd-46d9-95e0-ea72dd3d4dfc
md"""
Que se dividem nos seguintes tipos concretos, eles sÃ£o bastante autoexplicativos:
"""

# â•”â•â•¡ ab016abb-10ed-41cf-94a5-070440e96940
subtypes(DatePeriod)

# â•”â•â•¡ 550f7cec-138d-40af-886c-878b8a17b974
subtypes(TimePeriod)

# â•”â•â•¡ f0f81619-b99e-4329-9d7e-5d4831712937
md"""
Assim, poderÃ­amos, alternativamente, construir a hora oficial de nascimento do professor como:
"""

# â•”â•â•¡ 08eb4bd9-b812-40ee-b941-8c9ccde84771
DateTime(Year(1987), Month(9), Day(13), Hour(21), Minute(21))

# â•”â•â•¡ b3621dc3-c897-4673-a15b-f79e993d0018
md"""
## Parseando Datas

Na maioria das vezes, nÃ£o construiremos instÃ¢ncias de `Date` ou `DateTime` do zero. Na verdade, provavelmente iremos **parsear strings como tipos `Date` ou `DateTime`**.

Os construtores `Date` e `DateTime` podem ser alimentados com uma string e uma string de formato. Por exemplo, a string `"19870913"` que representa 1Âº de janeiro pode ser analisada com:
"""

# â•”â•â•¡ eb05091f-a13b-453a-b1bc-6a86b3e2a044
Date("19870913", "yyyymmdd")

# â•”â•â•¡ facc2961-abb3-4452-862e-fb5917a37ef8
md"""
!!! tip "ğŸ’¡ DateFormat"
    VocÃª pode encontrar mais informaÃ§Ãµes sobre como especificar formatos diferentes como strings na [documentaÃ§Ã£o de Julia `Dates`](https://docs.julialang.org/en/v1/stdlib/Dates/#Dates.DateFormat). NÃ£o se preocupe se vocÃª tiver que revisitÃ¡-lo o tempo todo, eu mesmo tenhop que fazer isso o tempo todo ao trabalhar com datas e *timestamps* de data/hora.
"""

# â•”â•â•¡ 7733271e-5010-485e-b861-6b4382fbbd5e
md"""
Observe que o segundo argumento Ã© uma representaÃ§Ã£o de string do formato. Temos os primeiros quatro dÃ­gitos representando o ano `y`, seguidos por dois dÃ­gitos para o mÃªs `m`, finalmente, dois dÃ­gitos para o dia `d`.

TambÃ©m funciona para *timestamps* de data/hora com `DateTime`:
"""

# â•”â•â•¡ 44c0f847-5084-4b88-9398-e14765d96dcf
DateTime("1987-09-13T21:21:00", "yyyy-mm-ddTHH:MM:SS")

# â•”â•â•¡ 17dd18e2-1b0d-4f80-8634-cdb468ddf8db
md"""
!!! tip "ğŸ’¡ Desempenho de DateFormat"
    De acordo com a [documentaÃ§Ã£o de Julia `Dates`](https://docs.julialang.org/en/v1/stdlib/Dates/#Constructors), usar o mÃ©todo `Date(date_string, format_string)` Ã© adequado se for chamado **apenas algumas vezes**.

	Se houver muitas strings de data formatadas de forma semelhante para analisar, no entanto, Ã© **muito mais eficiente** criar primeiro um tipo `DateFormat` e depois passÃ¡-lo em vez de uma string de formato bruto. Portanto, nosso exemplo anterior seria:

	```julia
	format = DateFormat("yyyymmdd")
	Date("19870913", format)
	```

	Como alternativa, **sem perda de desempenho**, vocÃª pode usar o prefixo literal de string `dateformat"..."`:

	```julia
	Date("19870913", dateformat"yyyymmdd")
	```
"""

# â•”â•â•¡ 8e3b0051-24a1-43a0-b58f-de0086cb4d78
md"""
## Extraindo InformaÃ§Ãµes de Datas

Ã‰ fÃ¡cil **extrair as informaÃ§Ãµes desejadas dos objetos `Date` e `DateTime`**.

Primeiro, vamos criar uma instÃ¢ncia de uma data muito especial:
"""

# â•”â•â•¡ d7d4c668-5026-4702-b3d2-184d727197d0
my_birthday = Date("1987-09-13")

# â•”â•â•¡ cb1fb301-21b2-456b-927f-9e2f2de4e159
md"""
Podemos extrair o que quisermos de `my_birthday`:
"""

# â•”â•â•¡ 5055226f-477a-4fb9-b72c-aada4d954abb
year(my_birthday)

# â•”â•â•¡ 41abfd3c-72c8-40ca-8e64-57bbcdd173af
month(my_birthday)

# â•”â•â•¡ 7c2fe723-88be-4582-aab0-e24fa76fb897
day(my_birthday)

# â•”â•â•¡ 487d0a02-907b-4454-b143-1b18bddd950c
md"""
O mÃ³dulo `Dates` de Julia tambÃ©m tem **funÃ§Ãµes compostas que retornam uma tupla de valores**:
"""

# â•”â•â•¡ 250bfa4d-0e2a-408b-9a8e-a4553c7ce4dc
yearmonth(my_birthday)

# â•”â•â•¡ cbd76bd2-8741-4306-a30a-64c4d42c24a5
monthday(my_birthday)

# â•”â•â•¡ 3ea5d363-d626-4999-95eb-a83a54e39343
yearmonthday(my_birthday)

# â•”â•â•¡ 854fb72f-4cbd-4e5c-9068-51cd78b3c946
md"""
TambÃ©m podemos ver o dia da semana e outras coisas Ãºteis:
"""

# â•”â•â•¡ 4b1bb682-249d-47f2-bcf7-917b402cc4bd
dayofweek(my_birthday)

# â•”â•â•¡ 1a77baaf-ae62-49ef-8807-2f7b48f01974
dayname(my_birthday)

# â•”â•â•¡ 1dfe096a-a614-4b12-b85e-33fc4edab1f5
dayofweekofmonth(my_birthday) # segundo domingo

# â•”â•â•¡ 27d182d9-d6ed-4978-b250-34c814a87ba1
md"""
> Sim eu nasci no segundo domingo de Setembro.
"""

# â•”â•â•¡ 66a8222a-6d38-464f-8466-59ef86f863ea
md"""
!!! tip "ğŸ’¡ Dias da Semana"
    Aqui estÃ¡ uma dica Ãºtil para recuperar apenas os dias da semana de oobjetos `Date`s. Basta usar um filtro em `dayofweek(sua_data) <= 5`.

	Para dias Ãºteis vocÃª pode usar o pacote [`BusinessDays.jl`](`BusinessDays.jl`).
"""

# â•”â•â•¡ a1ab378f-880a-4305-ae18-7bdbe28bef2a
md"""
## OperaÃ§Ãµes com Datas

Podemos realizar **operaÃ§Ãµes** em objetos de `Date`s.

Por exemplo, podemos adicionar dias a uma instÃ¢ncia de `Date` ou `DateTime`. Observe que as datas de Julia realizarÃ£o automaticamente os ajustes necessÃ¡rios para anos bissextos, de meses com 30 ou 31 dias (isso Ã© conhecido como aritmÃ©tica *calendrical*)
"""

# â•”â•â•¡ 8e369a6d-a489-4d5d-a3fe-8b3c40a37119
my_birthday + Day(90)

# â•”â•â•¡ 750c5d4b-1167-4457-bc56-9917db2dee17
md"""
Podemos adicionar quantos quisermos:
"""

# â•”â•â•¡ 0060e198-2eed-4fc2-83a7-d03d1d63a88b
my_birthday + Day(90) + Month(2) + Year(1)

# â•”â•â•¡ 91c162d8-f325-455e-a5c8-84b5fc74eb4a
md"""
Para obter a **duraÃ§Ã£o da data**, usamos apenas o operador de subtraÃ§Ã£o `-`. Vamos ver quantos dias JosÃ© tem:
"""

# â•”â•â•¡ 88bff547-e22d-408c-a440-5eceae0319e7
today() - my_birthday

# â•”â•â•¡ f07fa3b4-89fc-47f1-97d8-a708e72be343
md"""
A **duraÃ§Ã£o padrÃ£o** dos tipos de `Date` Ã© um objeto `Day`. Para `DateTime`, a **duraÃ§Ã£o padrÃ£o** Ã© um objeto `Millisecond`:
"""

# â•”â•â•¡ 499b7a68-9e50-4662-9033-9a19b9434fd8
DateTime(today()) - DateTime(my_birthday)

# â•”â•â•¡ 01108d2b-37e7-42f5-a2ff-5fca1167c891
md"""
## Intervalos de Datas

Uma coisa boa sobre o mÃ³dulo `Dates` Ã© que tambÃ©m podemos construir facilmente **intervalos de data e hora**.

Julia Ã© inteligente o suficiente para nÃ£o precisar definir todos os tipos de intervalo e operaÃ§Ãµes que cobrimos na [Aula 1 - Linguagem Julia e Estrutura de Dados Nativas](https://storopoli.io/Computacao-Cientifica/1_Julia/). Julia apenas estende as funÃ§Ãµes e operaÃ§Ãµes definidas para `UnitRange` para os tipos de `Date`.

> Isso Ã© conhecido como despacho mÃºltiplo e jÃ¡ abordamos na [Aula 1 - Linguagem Julia e Estrutura de Dados Nativas](https://storopoli.io/Computacao-Cientifica/1_Julia/).

Por exemplo, suponha que vocÃª deseja criar um intervalo de `Day`. Isso Ã© feito facilmente com o operador dois pontos `:`
"""

# â•”â•â•¡ 3346348c-2252-4472-805c-14594469fdbb
Date("2021-01-01"):Day(1):Date("2021-01-07")

# â•”â•â•¡ 3b1d0c01-ed90-493a-8d38-81c92bd4b4f9
md"""
NÃ£o hÃ¡ nada de especial em usar o `Day(1)` como intervalo, podemos usar **qualquer tipo de `Period` como intervalo**. Por exemplo, usando 3 dias como intervalos:
"""

# â•”â•â•¡ 387e3be7-2795-4a04-9b95-5b637286a544
Date("2021-01-01"):Day(3):Date("2021-01-07")

# â•”â•â•¡ d840124d-c074-474c-b64c-19b5ec70a11b
md"""
Ou atÃ© meses:
"""

# â•”â•â•¡ 63306f0b-46c5-43e2-8599-2e566e2e5cf4
Date("2021-01-01"):Month(1):Date("2021-03-01")

# â•”â•â•¡ 9c04f616-4485-4b7c-ad85-06d6e0d20312
md"""
Observe que o **tipo deste intervalo Ã© um `StepRange` com o tipo `Date` e tipo concretoo de `Period`** que usamos como intervalo dentro do operador dois pontos `:`
"""

# â•”â•â•¡ a72d77cf-02f3-483b-a4e1-62ceb12008be
my_date_interval = Date("2021-01-01"):Month(1):Date("2021-03-01")

# â•”â•â•¡ 8f721ddf-ba94-4d00-a009-8810f664a937
typeof(my_date_interval)

# â•”â•â•¡ fe16de37-e8c3-4077-a888-95aefe0c2d80
md"""
Podemos converter isso em um vetor com a funÃ§Ã£o `collect`:
"""

# â•”â•â•¡ f378ce11-8e0b-49c3-89e4-20939b1f5f14
my_date_interval_vector = collect(my_date_interval)

# â•”â•â•¡ 0bae3db9-9414-4b26-9f6f-bb503e0717ea
md"""
E ter todas as **funcionalidades de array disponÃ­veis**, como, por exemplo, indexaÃ§Ã£o:
"""

# â•”â•â•¡ 20f6a1ce-93cd-43e3-8bac-6e9c6cf81d2a
my_date_interval_vector[end]

# â•”â•â•¡ 0f4ec212-0022-4922-bf30-f0fe10f5e76e
md"""
TambÃ©m podemos **vetorizar (*broadcast*) operaÃ§Ãµes de data** para o nosso vetor de `Date`s:
"""

# â•”â•â•¡ 8314024f-9418-4420-819b-9ac96b2e36c5
my_date_interval_vector .+ Day(10)

# â•”â•â•¡ dac3e963-6470-41c0-8243-45d9561189f7
md"""
# O que Fazemos com Dados Temporais?

O mais importante Ã© colocar eles em um `DataFrame` (veja [Aula 4 - Dados Tabulares com DataFrames.jl](https://storopoli.io/Computacao-Cientifica/4_DataFrames/)).

Depois disso tem vÃ¡rias coisas que podemos fazer....
"""

# â•”â•â•¡ e0490ded-2eea-45e6-ad09-984f329839e9
md"""
## Usar as Funcionalidades do MÃ³dulo `Dates`

Podemos tambÃ©m usar as funcionalidades de `Dates` para manipular dados:
"""

# â•”â•â•¡ 4ffbc763-16a4-4a63-90ea-ee8466423e75
transform!(ap,
	:Date => ByRow(dayofweek) => :DayWeek,
	:Date => ByRow(month) => :Month)

# â•”â•â•¡ e4ea794a-4a6b-4a63-808b-fc93cabb7971
md"""
!!! danger "âš ï¸ Cuidado com Month vs month"
    Lembra `Month` Ã© um construtor de um tipo `Month` e `month` Ã© um extrator de meses de um objeto `Date` ou `DateTime`.

	A sÃ­ntaxe CamelCase Ã© para `struct`s `type`s e `module`s. O resto Ã© snake_case.
"""

# â•”â•â•¡ 4bca7094-79b1-438e-a4cd-1b0035e8218d
combine(groupby(ap, :DayWeek), :Passengers => mean)

# â•”â•â•¡ 36674734-e58d-4984-88cc-bfad40a035d3
combine(groupby(ap, :Month), :Passengers => mean)

# â•”â•â•¡ 878ad6c7-cfca-4af9-b999-32c83bc90d10
md"""
## Calcular valores com base em Valores Anteriores -- [`ShiftedArrays.jl`](https://github.com/JuliaArrays/ShiftedArrays.jl)

* `lag(v::AbstractArray, n = 1; default = missing)`
* `lead(v::AbstractArray, n = 1; default = missing)`
"""

# â•”â•â•¡ 7c6e7292-c74f-4807-be07-44162a8cadd8
v = [1, 3, 5, 4]

# â•”â•â•¡ de4425ad-866d-4654-b187-25b68bda5ca7
lag(v)

# â•”â•â•¡ 3cbd6c11-ecce-401d-877b-a810118cdc7e
lead(v)

# â•”â•â•¡ fc9793b6-d8ce-4ec0-a1ae-cd594cb25de8
md"""
Funciona tambÃ©m com intervalos:
"""

# â•”â•â•¡ 93b4b297-e02f-418e-8569-8c9d1b290e0f
w = 1:2:9

# â•”â•â•¡ df80ab42-4ea9-494f-9764-c0e841fb6949
lag(w, 2)

# â•”â•â•¡ 71ba199b-b0e6-48c3-97ca-bd8277454f5d
lead(w, 2)

# â•”â•â•¡ d3b4e17e-6432-45b6-b0bf-993b63bf1165
transform(ap, :Passengers => lag => :PassengerLag)

# â•”â•â•¡ 89221be1-a1d8-43fe-9252-069a41b23703
transform(ap, :Passengers => (x -> lead(x, 2)) => :PassengerLead)

# â•”â•â•¡ 2627e2e2-96de-4812-9542-ff7a28cbdc2f
md"""
## Calcular EstatÃ­sticas MÃ³veis com base em Valores Anteriores -- [`RollingFunctions.jl`](https://github.com/JeffreySarnoff/RollingFunctions.jl)

FunÃ§Ãµes com dois prefixos:
* **`roll`**: retorna um vetor de tamanho `ndata - windowsize + 1`
* **`run`**: retorna um vetor de tamanho de `ndata`

FunÃ§Ãµes com vÃ¡rios sufixos:
* `min`, `max`, `mean`, `median`.
* `var`, `std`, `sem`, `mad`, `mad_normalized`.
* `skewness`, `kurtosis`, `variation`.
* `cor`, `cov`: correlaÃ§Ã£o e covariÃ¢ncia (sobre dois vetores de dados).
"""

# â•”â•â•¡ 77b5a188-e6a6-4822-9c41-786b7904bc21
md"""
!!! tip "ğŸ’¡ FunÃ§Ãµes Customizadas"
    AlÃ©m disso vocÃª pode usar suas **prÃ³prias funÃ§Ãµes criadas** para os dados com `rolling` e `running`:

	```julia
	rolling(function, data, windowsize)
	rolling(function, data1, data2, windowsize)
	```

	> OBS: tambÃ©m aceita dois vetores se for uma funÃ§Ã£o complexa. Veja a documentaÃ§Ã£o no [`README.md` do `RollingFunctions.jl`](https://github.com/JeffreySarnoff/RollingFunctions.jl#works-with-your-functions)
"""

# â•”â•â•¡ e2c22ba6-faf2-41c1-b1f8-5119ae1dc14e
(length(v), length(w))

# â•”â•â•¡ 1d77ec4b-d94a-487a-a910-fb6fc6aa6f6a
rollmean(v,1) # ndata - windowsize + 1

# â•”â•â•¡ b253f241-a503-4932-951b-6cd890bbd70b
rollmean(v,2) # ndata - windowsize + 1

# â•”â•â•¡ 87004299-9a20-4dca-aeaf-41d19bd772ed
rollmean(w,1) # ndata - windowsize + 1

# â•”â•â•¡ 825bbd9c-f084-4a5e-b55c-376571b37e51
runmean(v, 1) # ndata

# â•”â•â•¡ 3e448093-8265-4506-a450-c8fd21bb2931
runmean(v, 2) # ndata

# â•”â•â•¡ a902af22-1e73-479a-8dec-3dca65961694
runmean(w, 1)

# â•”â•â•¡ a8b4992d-4cd4-4338-80a9-887117847b79
md"""
## Decompor SÃ©ries Temporais

Como vocÃªs viram **sÃ©ries temporais** tem alguns componentes:

$$y_t = S_t + T_t + R_t$$

Onde:

*  $y_t$ Ã© a observaÃ§Ã£o no tempo $t$.
*  $T_t$ Ã© o componente de **tÃªndencia** no tempo $t$.
*  $S_t$ Ã© o componente **sazonal** no tempo $t$.
*  $R_t$ Ã© o componente **residual** no tempo $t$.


Tem vÃ¡rias maneiras de decompor uma sÃ©rie temporal:

* [**X-11**](https://otexts.com/fpp3/methods-used-by-official-statistics-agencies.html#x-11-method): originado no US Census Bureau e foi desenvolvido pela Statistics Canada. O mÃ©todo X-11 Ã© um processo iterativo, usando mÃ©dias mÃ³veis apropriadas para decompor uma sÃ©rie temporal.


* [**SEATS**](https://otexts.com/fpp3/methods-used-by-official-statistics-agencies.html#seats-method): **Se**asonal **E**xtraction in **A**RIMA **T**ime **S**eries. Procedimento foi desenvolvido no Banco da Espanha e agora Ã© amplamente utilizado por agÃªncias governamentais em todo o mundo.


* [**STL**](): **S**easonal and **T**rend decomposition using **L**OESS. E LOESS Ã© _**l**ocally **e**stimated **s**catterplot **s**moothing_ (basicamente uma interpolaÃ§Ã£o polinomial)
"""

# â•”â•â•¡ 6de89a61-a3d8-47f4-8c1d-0a5369da0bc5
begin
	n = 1000
	axb = LinRange(-1/2,pi+1/2,n)
	x = LinRange(0,pi,n)
	y = sin.(x) .+ rand(n)
	scatter(x, y; xlims=(-1/2,pi+1/2), ma=0.5, label="Dados", color=:grey)
	plot!(axb, loess(x,y,predict=axb); lw=4, la=0.7, label = "LOESS", color=:blue)
	plot!(x,sma(y,100,true); lw=4, la=0.7, label= "MÃ©dia Movel 100", color=:orange)
	title!("LOESS")
	xlabel!("\$t\$")
end

# â•”â•â•¡ 6d773bbf-579f-4035-b079-9a553e99e166
md"""
### STL -- **S**easonal and **T**rend decomposition using **L**OESS

STL tem vÃ¡rias **vantagens** sobre a decomposiÃ§Ã£o clÃ¡ssica e os mÃ©todos SEATS e X-11:

* Ao contrÃ¡rio do SEATS e do X-11, o STL lida com **qualquer tipo de sazonalidade**, nÃ£o apenas dados mensais e trimestrais.


* O componente **sazonal $S_t$ pode mudar ao longo do tempo** e a taxa de mudanÃ§a pode ser **controlada pelo usuÃ¡rio**.


* A **suavidade do ciclo de tendÃªncia $T_t$ tambÃ©m pode ser controlada pelo usuÃ¡rio**.


* Pode ser robusto para *outliers* (ou seja, o usuÃ¡rio pode especificar uma decomposiÃ§Ã£o robusta), de modo que **observaÃ§Ãµes incomuns ocasionais nÃ£o afetem as estimativas** do ciclo de tendÃªncia $T_t$ e componentes sazonais $S_t$. Eles irÃ£o, no entanto, afetar o componente residual $R_t$.

DecomposiÃ§Ã£o STL estÃ¡ disponÃ­vel no pacote [`Forecast.jl`](https://github.com/viraltux/Forecast.jl) com a funÃ§Ã£o `stl`:
"""

# â•”â•â•¡ 448a1eb3-0512-4e10-98c2-829e356ccea8
stl_ap = stl(ap.Passengers, 12; robust=true)

# â•”â•â•¡ 213f02d5-679e-4d05-9f42-fa047663af50
typeof(stl_ap)

# â•”â•â•¡ 2b674a78-f024-459b-b0c4-b4107730e7cd
plot(stl_ap)

# â•”â•â•¡ 90351d6b-7117-4339-9c32-e5b80832b48c
md"""
#  PreparaÃ§Ãµes de Dados TÃ­picas em SÃ©ries Temporais

AlÃ©m de colocar os dados em um `DataFrame`, muitas tÃ©cnicas de sÃ©ries temporais necessitam de um tratamento especial dos dados.
O pacote [`DataWrangler.jl`](https://github.com/viraltux/DataWrangler.jl) possui diversas funÃ§Ãµes com esse propÃ³sito:

* **ImputaÃ§Ã£o de dados faltantes**
* **NormalizaÃ§Ã£o de dados** (por escore $z$, min-max, softmax ou sigmoid)
* **DeteÃ§Ã£o de _outliers_** (observaÃ§Ãµes influentes)
"""

# â•”â•â•¡ 8b9f03ac-e046-4279-8067-3381ae08871f
md"""
!!! info "ğŸ’ DataWranger.jl"
    O Pacote [`DataWrangler.jl`](https://github.com/viraltux/DataWrangler.jl) foi criado e Ã© mantido pelo [viraltux](https://github.com/viraltux) que tambÃ©m Ã© o criador dos pacotes [`Forecast.jl`](https://github.com/viraltux/Forecast.jl) e [`ForecastPlots.jl`](https://github.com/viraltux/ForecastPlots.jl).
"""

# â•”â•â•¡ 0d8b34df-da80-4be8-b891-1ecc1243b1e1
let
	n = 1000
	x = sort(rand(n))*2*pi;
	y = Array{Union{Missing,Float64}}(undef,n);
	y[:] = sin.(x).+randn(n)/10;
	mid = vcat(100:150,300:350,600:650,950:1000);
	
	y[mid] .= missing;
	scatter(x,y; label="dados")
	
	ipy = impute(x,y; method = "normal")
	scatter!(x[mid],ipy[mid]; label = "imputed 'normal'", color=:white)
	
	ipy = impute(x,y)
	scatter!(x[mid],ipy[mid]; label = "imputed 'loess'", color=:black, markersize = 2)
	title!("ImputaÃ§Ã£o de Dados Faltantes com DataWrangler.jl")
end

# â•”â•â•¡ 907379d8-932c-48d2-8582-d1b4d6d41df0
let
	n = 1000
	x = sort(rand(n))*2*pi;
	y = Array{Union{Missing,Float64}}(undef,n);
	y[:] = sin.(x).+randn(n)/10
	mid = vcat(100:150,300:350,600:650,950:1000);
	y[mid] .= y[mid] .+ 2*(randn(length(mid)).+1)
	
	outlist = outlie(x,y)
	scatter(outlist, y[outlist]; color="blue", label="outliers",ms=6)
	scatter!(y,color="lightblue", label="dados")
	title!("IdentificaÃ§Ã£o de Outliers com DataWrangler.jl")
end

# â•”â•â•¡ c9583420-6b50-4afb-aa34-7b070fbd50d6
md"""
# Maneiras de Modelar SÃ©ries Temporais

- $(HTML("<s>Frequentista</s>")):
   * Livro [Forecastring 3a ediÃ§Ã£o](https://otexts.com/fpp3/)
   * Livro [Analysis of Financial Time Series 3a ediÃ§Ã£o](https://faculty.chicagobooth.edu/ruey-s-tsay/research/analysis-of-financial-time-series-3rd-edition)
   * Pacote [`AutoARIMA.jl`](https://github.com/pierrenodet/AutoARIMA.jl): Modelos ARIMA.
   * Pacote [`Forecast.jl`](https://github.com/viraltux/Forecast.jl): foco em prediÃ§Ã£o e visualizaÃ§Ãµes, apenas modelos AR, decomposiÃ§Ã£o STL.
- **Bayesiana**:
   * Livro [Bayesian Data Analysis 3a ediÃ§Ã£o](http://www.stat.columbia.edu/~gelman/book/)
   * Livro [Statistical Rethinking 2a ediÃ§Ã£o](https://xcelab.net/rm/statistical-rethinking/)
   * Manual do [`Stan`, seÃ§Ã£o 2 de Modelos de SÃ©ries Temporais](https://mc-stan.org/docs/stan-users-guide/time-series-chapter.html): ARIMA, GARCH, HMM etc.
   * VersÃ£o Bayesiana em `Stan` dos modelos do Livro Analysis of Financial Time Series 3a ediÃ§Ã£o: [`marcomarconi/AFTS_with_Stan`](https://github.com/marcomarconi/AFTS_with_Stan) e [blog](https://notimeforbayes.blogspot.com/)
   * Pacote [`Turing.jl`](https://github.com/TuringLang/Turing.jl): [meus tutoriais em inglÃªs](https://storopoli.io/Bayesian-Julia) e vamos falar sobre ele na [Aula 9 - Modelos ProbabilÃ­sticos Bayesianos com `Turing.jl`](https://storopoli.io/Computacao-Cientifica/9_Turing/)
"""

# â•”â•â•¡ 3d98552e-014c-4939-8e93-3b3536cd0898
md"""
!!! info "ğŸ’ SÃ©ries Temporais em Julia"
    Veja essa [lista de vÃ¡rios pacotes](https://discourse.julialang.org/t/time-series-in-julia-working-list/62539).
"""

# â•”â•â•¡ edc542c7-8f70-4fa3-abcf-c9162ced69a0
md"""
## _**A**uto**r**egressive_ -- AR

O prÃ³prio nome diz, ele **regride em sÃ­ prÃ³prio**, ou seja vocÃª usa os dados em um tempo anterior como coeficientes do modelo.

Por exemplo um modelo **AR(1)** Ã©:

$$y_t = \alpha + \beta \cdot y_{t-1} + \epsilon$$

Onde:

*  $y_t$: observaÃ§Ã£o $y$ no tempo $t$.
*  $y_{t-1}$: observaÃ§Ã£o $y$ no tempo $t-1$.
*  $\alpha$: constante (*intercept*).
*  $\beta$: coeficiente (*slope*).
*  $\epsilon$: erro ou resÃ­duo.

Por exemplo um modelo **AR(2)** Ã©:

$$y_t = \alpha + \boldsymbol{\beta}_1 \cdot y_{t-1} + \boldsymbol{\beta}_2 \cdot y_{t-2} + \epsilon$$

Onde:

*  $y_t$: observaÃ§Ã£o $y$ no tempo $t$.
*  $y_{t-1}$: observaÃ§Ã£o $y$ no tempo $t-1$.
*  $y_{t-2}$: observaÃ§Ã£o $y$ no tempo $t-2$.
*  $\alpha$: constante (*intercept*).
*  $\boldsymbol{\beta}$: vetor de coeficientes (*slopes*).
*  $\epsilon$: erro ou resÃ­duo.

De modo geral os modelos **AR($p$)** sÃ£o:

$$y_t = \alpha + \sum^p_{i=1} \boldsymbol{\beta}_i \cdot y_{t-i} + \epsilon$$
"""

# â•”â•â•¡ 05f271d7-8d3f-4aeb-917e-079defcd67e7
md"""
!!! tip "ğŸ’¡ ARCH e GARCH"
    Os modelos economÃ©tricos e financeiros de sÃ©ries temporais geralmente assumem heterocedasticidade: eles permitem que a escala dos termos de erro que definem a sÃ©rie varie ao longo do tempo. O modelo mais simples Ã© o _**A**uto**r**egressive **C**onditional **H**eteroscedasticity_ (ARCH) com extensÃµes para o _**G**eneralized **A**uto**r**egressive **C**onditional **H**eteroscedasticity_ (GARCH)
"""

# â•”â•â•¡ 37c4966e-6a4d-4f9d-905b-a806cc8b3ba6
md"""
## _**M**oving **A**verage_ -- MA

Um modelo de mÃ©dia mÃ³vel usa erros anteriores como preditores para resultados futuros. Para um modelo **MA($q$)** de mÃ©dia mÃ³vel de ordem $q$:

$$y_t = \alpha + \sum^q_{i=1} \boldsymbol{\theta}_i \cdot \boldsymbol{\epsilon}_{t-i}$$

Onde:

*  $y_t$: observaÃ§Ã£o $y$ no tempo $t$.
*  $\alpha$: constante (*intercept*).
*  $\boldsymbol{\theta}$: vetor de coeficientes (*slopes*) para o erro.
*  $\boldsymbol{\epsilon}$: vetor de erros ou resÃ­duos.
"""

# â•”â•â•¡ 43fc389e-3daf-4a2d-b1c1-45dc10b53b49
md"""
## _**A**uto**r**egressive **M**oving **A**verage_ -- ARMA

JuncÃ£o do **AR($p$)** com o **MA($q$)** em **ARMA($p,q$)**:

$$y_t = \alpha + \sum^p_{i=1} \boldsymbol{\beta}_i \cdot y_{t-i} + \sum^q_{i=1} \boldsymbol{\theta}_i \cdot \boldsymbol{\epsilon}_{t-i}$$

Onde:

*  $y_t$: observaÃ§Ã£o $y$ no tempo $t$.
*  $y_{t-i}$: observaÃ§Ã£o $y$ no tempo $t-i$.
*  $\alpha$: constante (*intercept*).
*  $\boldsymbol{\beta}$: vetor de coeficientes (*slopes*).
*  $\boldsymbol{\theta}$: vetor de coeficientes (*slopes*) para o erro.
*  $\boldsymbol{\epsilon}$: vetor de erros ou resÃ­duos.
"""

# â•”â•â•¡ d13d7df1-7970-4696-8159-a17ba2ab9b03
md"""
## _**A**uto**r**egressive **Integrated** **M**oving **A**verage_ -- ARIMA

Ã‰ o **ARMA($p,q$)** com uma mÃ©dia movel **integrada**.

Por exemplo, um **ARIMA($p,1,q$)**:

$$y_{t} - y_{t-1} = \alpha + \sum^p_{i=1} \boldsymbol{\beta}_i \cdot \left( y_{t-i} - y_{t-i-1} \right) + \sum^q_{i=1} \boldsymbol{\theta}_i \cdot \boldsymbol{\epsilon}_{t-i}$$

Onde:

*  $y_{t} - y_{t-1}$: diferenÃ§a entre observaÃ§Ã£o $y$ no tempo $t$ e observaÃ§Ã£o no tempo $t-i$.
*  $y_{t-i} - y_{t-i-1}$: diferenÃ§a entre observaÃ§Ã£o $y$ no tempo $t-i$ e observaÃ§Ã£o no tempo $t-i-1$
*  $\alpha$: constante (*intercept*).
*  $\boldsymbol{\beta}$: vetor de coeficientes (*slopes*).
*  $\boldsymbol{\theta}$: vetor de coeficientes (*slopes*) para o erro.
*  $\boldsymbol{\epsilon}$: vetor de erros ou resÃ­duos.

De maneira geral, os modelos **ARIMA($p,d,q$)** sÃ£o:

$$\sum^d_{i=1} \left( y_{t_{i}} - y_{t_{i-1}} \right) = \alpha + \sum^p_{i=1} \sum^d_{i=1} \boldsymbol{\beta}_i \cdot \left( y_{t_{i-1}} - y_{t_{i-2}} \right) + \sum^q_{i=1} \boldsymbol{\theta}_i \cdot \boldsymbol{\epsilon}_{t-i}$$

Onde:

*  $y_{t_{i}} - y_{t_{i-1}}$: diferenÃ§a entre observaÃ§Ã£o $y$ no tempo $t_i$ e observaÃ§Ã£o no tempo $t_{i-1}$.
*  $\alpha$: constante (*intercept*).
*  $\boldsymbol{\beta}$: vetor de coeficientes (*slopes*).
*  $\boldsymbol{\theta}$: vetor de coeficientes (*slopes*) para o erro.
*  $\boldsymbol{\epsilon}$: vetor de erros ou resÃ­duos.

Ou uma notaÃ§Ã£o alternativa:

$$\begin{equation}
  \begin{array}{c c c c}
    (1-\beta_1 y - \cdots - \beta_p y^p) & (1-y)^d y_{t} &= &\alpha + (1 + \theta_1 y + \cdots + \theta_q y^q)\epsilon_t\\
    {\uparrow} & {\uparrow} & &{\uparrow}\\
    \text{AR($p$)} & \text{$d$ diferenÃ§as} & & \text{MA($q$)}\\
  \end{array}
\end{equation}$$

### Tudo acaba em $(HTML("<s>pizza</s>")) ARIMA

Como vocÃªs podem ver... todos que vimos atÃ© agora sÃ£o casos especiais de algum **ARIMA($p,d,q$)**:

* **AR($p$)**: **ARIMA($p,0,0$)**
* **MA($q$)**: **ARIMA($0,0,q$)**
* **ARMA($p,q)$**: **ARIMA($p,0,q$)**
"""

# â•”â•â•¡ 4f08d209-8956-4f7b-bfcb-51dbf52dc135
md"""
Modelos **ARIMA** estÃ£o disponÃ­veis no pacote [`AutoARIMA.jl`](https://github.com/pierrenodet/AutoARIMA.jl) com as funÃ§Ãµes:

* `fit(Params, y)`: passa um dos construtores abaixo e um vetor $y$.
* `forecast(Model, y_new)`: passa o modelo retornado por `fit` e um vetor $y_{t+i}$

E os construtores:

* `ARParams(c::Bool, p)`: **AR($p$)**
* `MAParams(q)`: **MA($q$)**
* `ARMAParams(c::Bool, p, q)`: **ARMA($p,q$)**
* `ARIMAParams(c::Bool, p, d, q)`: **ARIMA($p,d,q$)**
"""

# â•”â•â•¡ 08a5e81b-72ad-4ed3-8bac-fa6d050c58c5
ar1 = ARParams(true, [1])

# â•”â•â•¡ 07a95924-d638-4b15-8b8a-b99af5254865
m_ar1 = fit(ar1, float.(ap.Passengers))

# â•”â•â•¡ b8939ebd-81bb-4e73-a6f2-7e1fe6ee471a
ma2 = MAParams([1, 2])

# â•”â•â•¡ f24dd12d-039c-470c-9d1c-5f84538c5e6a
m_ma2 = fit(ma2, float.(ap.Passengers))

# â•”â•â•¡ aa46d085-8757-4453-a34f-4d8891bce366
ar2ma3 = ARMAParams(true, [1, 2], [1, 3]) # notem que estou sem o 2 no q

# â•”â•â•¡ 7a8bc69d-f3c1-4dbb-a8e0-373ca39bb06a
m_ar2ma3 = fit(ar2ma3, float.(ap.Passengers))

# â•”â•â•¡ d68b6f54-8883-4453-95f2-533c528955ac
ar2i3ma3 = ARIMAParams(true, [1, 2], 3, [1, 2, 3]) # d nÃ£o Ã© vetor

# â•”â•â•¡ 7c8af0f8-9ef8-4ed2-8d35-82827567fb81
m_ar2i3ma3 = fit(ar2i3ma3, float.(ap.Passengers))

# â•”â•â•¡ 894c5e01-b7da-4f98-9594-fb06ec5d6642
forecast(m_ar1, float.(ap.Passengers)) # prÃ³ximo ponto

# â•”â•â•¡ ffa8b715-8974-481a-98c5-b5f101b0dff6
md"""
# FunÃ§Ãµes de GeraÃ§Ã£o de Dados SintÃ©ticos
"""

# â•”â•â•¡ 661a485e-5721-4acc-96a4-39674ec27c1e
function generate_fake_data(N::Int; seasonality=false, noise=false, stationary=true)
	values = collect(1:N) .* 0.4
	if seasonality
		repeats = Int(floor(N/50))
		values = collect(1:50) .* 0.25
		values[1:9] .^=2.105
		values[10:50] .= values[1:41] .^2
		values = repeat(values, repeats)
	end
	if noise
		values += (randn(N) .* 2.5)
	end
	if !stationary
		values += collect(1:N) .* 0.4
	end
	return values
end

# â•”â•â•¡ 849d7757-f5ff-41fc-9330-a83e063316b6
plot([
		generate_fake_data(N_2),
		generate_fake_data(N_2; seasonality=true),
		generate_fake_data(N_2; seasonality=true, noise=true),
		generate_fake_data(N_2; seasonality=true, noise=true, stationary=false)
		];
	layout=4, label=:none,
	title = ["TendÃªncia" "Sazonalidade" "RuÃ­do" "NÃ£o-EstacionÃ¡ria"] 
)

# â•”â•â•¡ d548bc1a-2e20-4b7f-971b-1b07faaa4c13
md"""
# Ambiente
"""

# â•”â•â•¡ 23974dfc-7412-4983-9dcc-16e7a3e7dcc4
with_terminal() do
	deps = [pair.second for pair in Pkg.dependencies()]
	deps = filter(p -> p.is_direct_dep, deps)
	deps = filter(p -> !isnothing(p.version), deps)
	list = ["$(p.name) $(p.version)" for p in deps]
	sort!(list)
	println(join(list, '\n'))
end

# â•”â•â•¡ 659b4a75-c348-4cfa-b76e-420a4e224730
md"""
# LicenÃ§a

Este conteÃºdo possui licenÃ§a [Creative Commons Attribution-ShareAlike 4.0 Internacional](http://creativecommons.org/licenses/by-sa/4.0/).

[![CC BY-SA 4.0](https://licensebuttons.net/l/by-sa/4.0/88x31.png)](http://creativecommons.org/licenses/by-sa/4.0/)
"""

# â•”â•â•¡ Cell order:
# â•Ÿâ”€cbc48ca5-f1a4-4e13-9323-2fd2c43d8612
# â•Ÿâ”€7bb67403-d2ac-4dc9-b2f1-fdea7a795329
# â•Ÿâ”€ff1ec432-ce73-4d04-8dcf-dea133894b67
# â• â•27f62732-c909-11eb-27ee-e373dce148d9
# â•Ÿâ”€b09513de-7df4-4fd5-95c4-d38965d6a666
# â•Ÿâ”€59b60d4a-04bf-425e-a443-1361af55a428
# â•Ÿâ”€9eb79017-d135-44e9-a2ea-712829418a6d
# â•Ÿâ”€38581a7b-2da4-49fb-b5e0-32db22cb3616
# â•Ÿâ”€23e24ad3-296b-4316-bb1b-617262b93a9b
# â•Ÿâ”€aa89ebab-573c-4819-b1c6-ffce3fff5a18
# â•Ÿâ”€f8ab9748-2002-4e48-82d2-60518e06f5cd
# â•Ÿâ”€849d7757-f5ff-41fc-9330-a83e063316b6
# â•Ÿâ”€5d701b31-c830-4097-95ef-0e257ea36cb0
# â•Ÿâ”€edc92911-9627-45d3-a340-4292ce4cfc3e
# â• â•c958db84-9cb5-4398-b37a-15e8eb0b19f5
# â•Ÿâ”€733645f3-70d2-4e75-9688-7170dd3e3d3b
# â•Ÿâ”€1a579b82-7552-4bba-bcaa-8eb507d34db4
# â• â•e78c9151-baa3-4ec3-95bc-36843a42305c
# â•Ÿâ”€d67b34ed-4fd8-4f4b-9e9b-027dcef8e05f
# â•Ÿâ”€d702c956-5765-433a-833a-fef06dacad8e
# â•Ÿâ”€8131495e-f21a-4473-bf7d-beb84c145636
# â• â•0e22fdf9-7106-4e03-b73e-df63e4612a8f
# â• â•baa42a2f-ac93-49ac-9af3-b794466f1b8f
# â• â•c085d76a-7d2e-48ea-820a-9baa303138b6
# â• â•3b05adc5-8a00-4807-9418-071326f10e18
# â• â•fef9271b-6956-4e5b-bc43-93501a05c2c1
# â•Ÿâ”€0f1481ac-a337-4ff1-bc2d-f810dc90fcfb
# â•Ÿâ”€15a25125-675a-4250-bfb5-56b982e63880
# â• â•cb6a5372-b6b2-48c7-ad48-30023cceac80
# â•Ÿâ”€a5e761fa-b3dd-46d9-95e0-ea72dd3d4dfc
# â• â•ab016abb-10ed-41cf-94a5-070440e96940
# â• â•550f7cec-138d-40af-886c-878b8a17b974
# â•Ÿâ”€f0f81619-b99e-4329-9d7e-5d4831712937
# â• â•08eb4bd9-b812-40ee-b941-8c9ccde84771
# â•Ÿâ”€b3621dc3-c897-4673-a15b-f79e993d0018
# â• â•eb05091f-a13b-453a-b1bc-6a86b3e2a044
# â•Ÿâ”€facc2961-abb3-4452-862e-fb5917a37ef8
# â•Ÿâ”€7733271e-5010-485e-b861-6b4382fbbd5e
# â• â•44c0f847-5084-4b88-9398-e14765d96dcf
# â•Ÿâ”€17dd18e2-1b0d-4f80-8634-cdb468ddf8db
# â•Ÿâ”€8e3b0051-24a1-43a0-b58f-de0086cb4d78
# â• â•d7d4c668-5026-4702-b3d2-184d727197d0
# â•Ÿâ”€cb1fb301-21b2-456b-927f-9e2f2de4e159
# â• â•5055226f-477a-4fb9-b72c-aada4d954abb
# â• â•41abfd3c-72c8-40ca-8e64-57bbcdd173af
# â• â•7c2fe723-88be-4582-aab0-e24fa76fb897
# â•Ÿâ”€487d0a02-907b-4454-b143-1b18bddd950c
# â• â•250bfa4d-0e2a-408b-9a8e-a4553c7ce4dc
# â• â•cbd76bd2-8741-4306-a30a-64c4d42c24a5
# â• â•3ea5d363-d626-4999-95eb-a83a54e39343
# â•Ÿâ”€854fb72f-4cbd-4e5c-9068-51cd78b3c946
# â• â•4b1bb682-249d-47f2-bcf7-917b402cc4bd
# â• â•1a77baaf-ae62-49ef-8807-2f7b48f01974
# â• â•1dfe096a-a614-4b12-b85e-33fc4edab1f5
# â•Ÿâ”€27d182d9-d6ed-4978-b250-34c814a87ba1
# â•Ÿâ”€66a8222a-6d38-464f-8466-59ef86f863ea
# â•Ÿâ”€a1ab378f-880a-4305-ae18-7bdbe28bef2a
# â• â•8e369a6d-a489-4d5d-a3fe-8b3c40a37119
# â•Ÿâ”€750c5d4b-1167-4457-bc56-9917db2dee17
# â• â•0060e198-2eed-4fc2-83a7-d03d1d63a88b
# â•Ÿâ”€91c162d8-f325-455e-a5c8-84b5fc74eb4a
# â• â•88bff547-e22d-408c-a440-5eceae0319e7
# â•Ÿâ”€f07fa3b4-89fc-47f1-97d8-a708e72be343
# â• â•499b7a68-9e50-4662-9033-9a19b9434fd8
# â•Ÿâ”€01108d2b-37e7-42f5-a2ff-5fca1167c891
# â• â•3346348c-2252-4472-805c-14594469fdbb
# â•Ÿâ”€3b1d0c01-ed90-493a-8d38-81c92bd4b4f9
# â• â•387e3be7-2795-4a04-9b95-5b637286a544
# â•Ÿâ”€d840124d-c074-474c-b64c-19b5ec70a11b
# â• â•63306f0b-46c5-43e2-8599-2e566e2e5cf4
# â•Ÿâ”€9c04f616-4485-4b7c-ad85-06d6e0d20312
# â• â•a72d77cf-02f3-483b-a4e1-62ceb12008be
# â• â•8f721ddf-ba94-4d00-a009-8810f664a937
# â•Ÿâ”€fe16de37-e8c3-4077-a888-95aefe0c2d80
# â• â•f378ce11-8e0b-49c3-89e4-20939b1f5f14
# â•Ÿâ”€0bae3db9-9414-4b26-9f6f-bb503e0717ea
# â• â•20f6a1ce-93cd-43e3-8bac-6e9c6cf81d2a
# â•Ÿâ”€0f4ec212-0022-4922-bf30-f0fe10f5e76e
# â• â•8314024f-9418-4420-819b-9ac96b2e36c5
# â•Ÿâ”€dac3e963-6470-41c0-8243-45d9561189f7
# â•Ÿâ”€e0490ded-2eea-45e6-ad09-984f329839e9
# â• â•4ffbc763-16a4-4a63-90ea-ee8466423e75
# â•Ÿâ”€e4ea794a-4a6b-4a63-808b-fc93cabb7971
# â• â•4bca7094-79b1-438e-a4cd-1b0035e8218d
# â• â•36674734-e58d-4984-88cc-bfad40a035d3
# â•Ÿâ”€878ad6c7-cfca-4af9-b999-32c83bc90d10
# â• â•7c6e7292-c74f-4807-be07-44162a8cadd8
# â• â•de4425ad-866d-4654-b187-25b68bda5ca7
# â• â•3cbd6c11-ecce-401d-877b-a810118cdc7e
# â•Ÿâ”€fc9793b6-d8ce-4ec0-a1ae-cd594cb25de8
# â• â•93b4b297-e02f-418e-8569-8c9d1b290e0f
# â• â•df80ab42-4ea9-494f-9764-c0e841fb6949
# â• â•71ba199b-b0e6-48c3-97ca-bd8277454f5d
# â• â•d3b4e17e-6432-45b6-b0bf-993b63bf1165
# â• â•89221be1-a1d8-43fe-9252-069a41b23703
# â•Ÿâ”€2627e2e2-96de-4812-9542-ff7a28cbdc2f
# â•Ÿâ”€77b5a188-e6a6-4822-9c41-786b7904bc21
# â• â•e2c22ba6-faf2-41c1-b1f8-5119ae1dc14e
# â• â•1d77ec4b-d94a-487a-a910-fb6fc6aa6f6a
# â• â•b253f241-a503-4932-951b-6cd890bbd70b
# â• â•87004299-9a20-4dca-aeaf-41d19bd772ed
# â• â•825bbd9c-f084-4a5e-b55c-376571b37e51
# â• â•3e448093-8265-4506-a450-c8fd21bb2931
# â• â•a902af22-1e73-479a-8dec-3dca65961694
# â•Ÿâ”€a8b4992d-4cd4-4338-80a9-887117847b79
# â•Ÿâ”€6de89a61-a3d8-47f4-8c1d-0a5369da0bc5
# â•Ÿâ”€6d773bbf-579f-4035-b079-9a553e99e166
# â• â•448a1eb3-0512-4e10-98c2-829e356ccea8
# â• â•213f02d5-679e-4d05-9f42-fa047663af50
# â• â•2b674a78-f024-459b-b0c4-b4107730e7cd
# â•Ÿâ”€90351d6b-7117-4339-9c32-e5b80832b48c
# â•Ÿâ”€8b9f03ac-e046-4279-8067-3381ae08871f
# â•Ÿâ”€0d8b34df-da80-4be8-b891-1ecc1243b1e1
# â•Ÿâ”€907379d8-932c-48d2-8582-d1b4d6d41df0
# â•Ÿâ”€c9583420-6b50-4afb-aa34-7b070fbd50d6
# â•Ÿâ”€3d98552e-014c-4939-8e93-3b3536cd0898
# â•Ÿâ”€edc542c7-8f70-4fa3-abcf-c9162ced69a0
# â•Ÿâ”€05f271d7-8d3f-4aeb-917e-079defcd67e7
# â•Ÿâ”€37c4966e-6a4d-4f9d-905b-a806cc8b3ba6
# â•Ÿâ”€43fc389e-3daf-4a2d-b1c1-45dc10b53b49
# â•Ÿâ”€d13d7df1-7970-4696-8159-a17ba2ab9b03
# â•Ÿâ”€4f08d209-8956-4f7b-bfcb-51dbf52dc135
# â• â•08a5e81b-72ad-4ed3-8bac-fa6d050c58c5
# â• â•07a95924-d638-4b15-8b8a-b99af5254865
# â• â•b8939ebd-81bb-4e73-a6f2-7e1fe6ee471a
# â• â•f24dd12d-039c-470c-9d1c-5f84538c5e6a
# â• â•aa46d085-8757-4453-a34f-4d8891bce366
# â• â•7a8bc69d-f3c1-4dbb-a8e0-373ca39bb06a
# â• â•d68b6f54-8883-4453-95f2-533c528955ac
# â• â•7c8af0f8-9ef8-4ed2-8d35-82827567fb81
# â• â•894c5e01-b7da-4f98-9594-fb06ec5d6642
# â•Ÿâ”€ffa8b715-8974-481a-98c5-b5f101b0dff6
# â• â•661a485e-5721-4acc-96a4-39674ec27c1e
# â•Ÿâ”€d548bc1a-2e20-4b7f-971b-1b07faaa4c13
# â•Ÿâ”€228e9bf1-cfd8-4285-8b68-43762e1ae8c7
# â•Ÿâ”€23974dfc-7412-4983-9dcc-16e7a3e7dcc4
# â•Ÿâ”€659b4a75-c348-4cfa-b76e-420a4e224730
